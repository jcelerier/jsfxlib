desc:SonicDelaySphere v1.0.2 (NovaSonica)
in_pin:Left
in_pin:Right
out_pin:Left
out_pin:Right
slider1:0.75<0,16,0.015625>-Length
slider4:1<0,1,0.01>-Feedback
slider8:1<0,1,0.01>-Fwd
slider9:1<0,1,0.01>-Rev
slider11:1<0,1,0.01>-Dry
slider12:1<0,1,0.01>-Wet
slider13:2.5<0,4,0.0625>-InfraSound Clean
slider14:0.9375<0,4,0.0625>-UltraSound Clean
slider15:1<0,1,0.01>-Gain Rider
slider16:1000<0,20000,1>-Detector Freq.

/*||||||||||||||||||||||||||||||||||||||*/

@init
/*||||||||||||||||||||||||||||||||||||||*/
GUI_RENDER_GRID_BACKGROUND = 1;
GUI_RENDER_SUBGRID_BACKGROUND = 1;
GUI_RENDER_FRAME = 1;
GUI_RENDER_COMPONENTS = 1;
GUI_RENDER_DEBUG_BORDERS = 0;
THEME_COLORS_SHIFT_HOVER_HUE = -285;
THEME_COLORS_SHIFT_FOREGROUND_HUE = 130;
THEME_COLORS_SHIFT_LABEL_HUE = -30;
THEME_COLORS_SHIFT_DISPLAY_HUE = 5;
THEME_COLORS_MAIN_HUE = 216;
THEME_COLORS_MAIN_SAT = 0.49;
THEME_COLORS_MAIN_LUM = 0.26;
THEME_COLORS_SUB_HUE = 216;
THEME_COLORS_SUB_SAT = 0.47;
THEME_COLORS_SUB_SAT_DIM = THEME_COLORS_SUB_SAT * 0.90;
THEME_COLORS_SUB_LUM = 0.25;
THEME_COLORS_SUB_LUM_DIM = THEME_COLORS_SUB_LUM * 0.90;
THEME_COLORS_SUB_HUE_FG = THEME_COLORS_MAIN_HUE + THEME_COLORS_SHIFT_FOREGROUND_HUE;
THEME_COLORS_SUB_HUE_HOVER = THEME_COLORS_SUB_HUE + THEME_COLORS_SHIFT_FOREGROUND_HUE;
THEME_COLORS_SUB_HUE_LABEL = THEME_COLORS_SUB_HUE + THEME_COLORS_SHIFT_LABEL_HUE;
THEME_COLORS_DISPLAY_HUE = THEME_COLORS_SUB_HUE + THEME_COLORS_SHIFT_DISPLAY_HUE;
THEME_COLORS_DISPLAY_SAT = 0.5;
THEME_COLORS_DISPLAY_LUM = 0.5;
THEME_PADDING_MAIN_LEFT = 7;
THEME_PADDING_MAIN_RIGHT = 7;
THEME_PADDING_MAIN_TOP = 7;
THEME_PADDING_MAIN_BOTTOM = 7;
THEME_PADDING_SUB = 7;
THEME_FRAME_LEFT = 6;
THEME_FRAME_RIGHT = 6;
THEME_FRAME_TOP = 6;
THEME_FRAME_BOTTOM = 6;
BUFFERS_MAX = 100;
GFX_BUFFERS_MAX = 1000;
GFX_BUFFER_SIZE = 11;
UI_BUFFER_SIZE = 25;
GONIOMETER_BUFFER_SIZE = 2048;
UI_BUFFER_ADDR_BASE = 16384*10;
UI_BUFFER_ADDR_MAIN = UI_BUFFER_ADDR_BASE;
UI_BUFFER_ADDR_SUB = UI_BUFFER_ADDR_BASE + UI_BUFFER_SIZE;
GFX_BUFFER_ADDR_BASE = UI_BUFFER_ADDR_BASE + UI_BUFFER_SIZE * 4;
GFX_OFF_SCREEN_ADDR_BASE = GFX_BUFFER_ADDR_BASE + GFX_BUFFERS_MAX * GFX_BUFFER_SIZE;
GONIOMETER_BUFFER_ADDR_BASE = GFX_OFF_SCREEN_ADDR_BASE + GFX_BUFFERS_MAX * GFX_BUFFER_SIZE;
GONIOMETER_BUFFER_ADDR_X = GONIOMETER_BUFFER_ADDR_BASE;
GONIOMETER_BUFFER_ADDR_Y = GONIOMETER_BUFFER_ADDR_X + GONIOMETER_BUFFER_SIZE * 2 + 2;
BUFFER_ADDR_BASE = GONIOMETER_BUFFER_ADDR_Y + GONIOMETER_BUFFER_SIZE * 2 + 2;
BUFFER_ADDR_HEADERS = BUFFER_ADDR_BASE;
BUFFER_ADDR_ADDRESSES = BUFFER_ADDR_BASE + BUFFERS_MAX;
BUFFER_ADDR_DATA = BUFFER_ADDR_BASE + BUFFERS_MAX * 2;

function status()
(
	this.status_last = 0;
	this.status_changed = 0;
	this.samples_played = 0;
);

function status_is_playing()
(
	play_state == 1 || play_state == 5;
);

function status_update()
(
	this.status_changed = play_state != this.status_last;
	this.status_changed ?
	(
		this.samples_played = 0;
	) :
	(
		this.status_is_playing() ?
		(
			this.samples_played += 1;
		);
	);
	this.status_last = play_state;
);
ST.status();

function smoothing_rms(ms)
(
	this.coeff = exp(-1/(ms / 1000 * srate));
	this.icoeff = 1-this.coeff;
);

function smoothing_rms_sample(input)
(
	this.rms_s = (this.rms_s * this.coeff) + (this.icoeff * input * input);
	sqrt(this.rms_s);
);

function smoothing_single_pole(ms blocksize)
(
	this.coeff = exp(-1/(ms / 1000 * srate / blocksize));
	this.icoeff = 1-this.coeff;
);

function smoothing_single_pole_sample(input target)
(
	input * this.coeff + target * this.icoeff;
);
PI = $pi;
TWO_PI = $pi*2;
FOUR_PI = $pi*4;
TENTH_PI = $pi/10;
COS_OF_ONE = cos(1);
SIN_OF_ONE = sin(1);
TWELFTH_ROOT_OF_TWO = 2^(1/12);
TUNING = 440;
phi = 1.6803398875;
phi2 = 1.6803398875+1.6803398875;
phi4 = phi2+phi2;
half_pi = $pi/2;
one_and_a_half_pi = $pi*1.5;
pi2 = 2 * $pi;

function db2vol(db)
(
	10 ^ (db / 20)
);

function clamp(x xMin xMax)
(
	min(xMax,max(xMin,x));
);

function clampNormCenter(x)
(
	min(1,max(-1,x));
);

function samplesNBars(n)
(
	ceil(60 * n * ts_num ^ 2 * srate / (tempo * ts_denom));
);

function fmod(x y)
local(xy)
(
	xy = x/y;
	abs(floor(xy) - xy)*2;
);

function range(min max)
(
	min < 0 && max > 0 ? abs(min)+abs(max) : max(abs(max), abs(min)) - min(abs(max), abs(min));
);

function normalize(val min max)
local(range)
(
	range = range(min, max);
	val/range - min/range;
);

function denormalize(val min max)
(
	val * range(min, max) + min;
);

function zeroCrossing(min max)
(
	-min / range(min, max);
);

function round(val)
(
	floor(val + 0.5);
);

function round(val stepsize)
(
	round(val/stepsize)*stepsize;
);

function snapNormalizedToEdge(val snapDistance)
local(minSnap maxSnap)
(
	minSnap = snapDistance;
	maxSnap = 1 - snapDistance;
	val <= minSnap ? ( val = 0; );
	val >= maxSnap ? ( val = 1; );
	val;
);

function snapXToPixels(x)
(
	x|0 + snapNormalizedToEdge(x-floor(x), 0.02);
);

function snapYToPixels(y)
(
	y|0 + snapNormalizedToEdge(y-floor(y), 0.02);
);

function snapWidthToPixels(w)
(
	w|0 + snapNormalizedToEdge(w-floor(w), 0.02);
);

function snapHeightToPixels(h)
(
	h|0 + snapNormalizedToEdge(h-floor(h), 0.02) + 1;
);

function signal()
(
	this.sample = 0;
	this.sample_plus_dc = 0;
	this.sample_minus_dc = 0;
	this.average = 0;
	this.variance = 0;
	this.dc_offset = 0;
	this.average_fast = 0;
	this.average_slow = 0;
	this.envelope = 0;
);

function signal_envelope(input w_dc w_env)
(
	this.sample = input;
	this.dc_offset = w_dc * this.sample + (1 - w_dc) * this.v;
	this.envelope = w_env * abs(this.sample - this.dc_offset) + (1 - w_env) * this.envelope;
	this.envelope;
);

function mouseInArea(x y w h)
(
	mouse_y >= y && mouse_y <= (y+h) && (mouse_x >= x && mouse_x <= (x+w));
);

function mouseClickIn(x y w h)
(
	mouse_cap == 1 && mouseInArea(x,y,w,h);
);

function mouseALTClickIn(x y w h)
(
	mouse_cap == 17 && mouseInArea(x,y,w,h);
);

function mouseWheelIn(x y w h)
(
	mouse_wheel != 0 && mouseInArea(x,y,w,h);
);

function hsla(h s l a)
local(m c x r g b quadrant)
(
	h < 0 ?
	(
		h = h + ceil(-h / 360) * 360;
	) :
	(
		h > 360 ?
		(
			h = h - floor(h / 360) * 360;
		);
	);
	h == 0 ? h = 360;
	m = l - (c / 2);
	c = (1 - abs(2 * s - 1)) * s;
	x = c * (1 - abs(fmod((h / 60), 2) - 1));
	r = 0; g = 0; b = 0;
	quadrant = ceil(h / 60);
	quadrant == 1 ? ( r = c; g = x; b = 0; );
	quadrant == 2 ? ( r = x; g = c; b = 0; );
	quadrant == 3 ? ( r = 0; g = c; b = x; );
	quadrant == 4 ? ( r = 0; g = x; b = c; );
	quadrant == 5 ? ( r = x; g = 0; b = c; );
	quadrant == 6 ? ( r = c; g = 0; b = x; );
	gfx_r = r + m;
	gfx_g = g + m;
	gfx_b = b + m;
	gfx_a = a;
);

function blurArea(x y w h n)
(
	loop(n,
		gfx_x = x;
		gfx_y = y;
		gfx_blurto(gfx_x+w, gfx_y+h);
	);
);

function drawString(x y str)
(
	gfx_x = x;
	gfx_y = y;
	gfx_drawstr(str);
);

function drawStringOutlined(x y str hue sat lum a)
(
	hsla(hue,0,0.01,a/2);
	drawString(x-1, y, str);
	drawString(x+1, y, str);
	drawString(x, y-1, str);
	drawString(x, y+1, str);
	drawString(x-1, y-1, str);
	drawString(x+1, y-1, str);
	drawString(x-1, y+1, str);
	drawString(x+1, y+1, str);
	hsla(hue,sat,lum,a);
	drawString(x, y, str);
);

function drawStringCentered(x y w h str hue sat lum a)
(
	gfx_measurestr(str, str_w, str_h);
	drawStringOutlined( x + w / 2 - str_w / 2,
	y + h / 2 - str_h / 2,
	str,
	hue,
	sat,
	lum,
	a);
);

function formatNumber(num precision unit maxLen)
local(str format)
(
	str = #;
	format = #;
	precision > 0 ? sprintf(format, "%%.%d%s%%s", precision, "f")
	: sprintf(format, "%%%d%s%%s", precision, "d");
	sprintf(str, format, num, unit);
	sprintf(format, "%%%ds", maxLen);
	sprintf(str, format, str);
	str;
);

function showToolTip(x y w h str)
local(strW strH bx by bw bh)
(
	mouse_y >= y && mouse_y <= (y+h) && (mouse_x >= x && mouse_x <= (x+w)) ?
	(
		strW = 0;
		strH = 0;
		gfx_measurestr(str, strW, strH);
		bx = mouse_x + 10;
		by = mouse_y - strH - 15;
		bw = strW + 5;
		bh = strH + 5;
		bx < x ? bx = x;
		by < y ? by = y;
		bx + bw >= x+w ? bx = x+w-bw;
		by + bh >= y+h ? by = y+h-bh;
		blurArea(bx, by, bw, bh, 16);
		gfx_r = 0; gfx_g = 0; gfx_b = 0; gfx_a = 0.5;
		gfx_rect(bx, by, bw, bh);
		gfx_r = 1; gfx_g = 1; gfx_b = 1; gfx_a = 1;
		gfx_rect(bx-1, by-1, bw+2, bh+2, -1);
		drawString(bx+2.5, by+2.5, str);
	);
);

function blit_prepare_display(hue sat lum alpha)
local(last_h last_s last_l last_a)
(
	last_h != hue || last_s != sat || last_l != lum || last_a != alpha ?
	(
		gfx_dest = 50;
		gfx_setimgdim(50, 2, 2);
		gfx_r = gfx_g = gfx_b = 0; gfx_a = 1;
		gfx_rect(0, 0, 2, 2);
		hsla(hue, sat, lum, alpha);
		gfx_x = 0; gfx_y = 0;
		gfx_setpixel(gfx_r, gfx_g, gfx_b);
		hsla(hue, sat, lum*0.5, alpha);
		gfx_x = 1; gfx_y = 1;
		gfx_setpixel(gfx_r, gfx_g, gfx_b);
		hsla(hue, sat, lum*(2/3), alpha);
		gfx_x = 1; gfx_y = 0;
		gfx_setpixel(gfx_r, gfx_g, gfx_b);
		gfx_x = 0; gfx_y = 1;
		gfx_setpixel(gfx_r, gfx_g, gfx_b);
		gfx_dest = -1;
		last_h = hue;
		last_s = sat;
		last_l = lum;
		last_a = alpha;
	);
);

function blit_tiled_display(x y w h hue sat lum alpha)
local(coordinatelist l m xpos ypos last_x last_y last_w last_h)
(
	last_x != x || last_y != y || last_w != w || last_h != h ?
	(
		blit_prepare_display(hue, sat, lum, alpha);
		x = ceil(x);
		y = ceil(y);
		coordinatelist=GFX_BUFFER_ADDR_BASE;
		coordinatelist[0]=0;
		coordinatelist[1]=0;
		coordinatelist[2]=2;
		coordinatelist[3]=2;
		coordinatelist[6]=2;
		coordinatelist[7]=2;
		coordinatelist[8]=0;
		coordinatelist[9]=0;
		l = floor(w);
		m = floor(h);
		i = 0;
		__n_i = ceil((l-0)/2);
		loop (__n_i,
			j = 0;
			__n_j = ceil((m-0)/2);
			loop (__n_j,
				xpos = x+i;
				ypos = y+j;
				coordinatelist[4]= xpos - xpos % 2;
				coordinatelist[5]= ypos - ypos % 2;
				gfx_blitext(50, coordinatelist, 0);
				j += 2;
			);
			i += 2;
		);
		last_x = x;
		last_y = y;
		last_w = w;
		last_h = h;
	);
);

function blit_border(ix iy iw ih)
(
	gfx_r = 1; gfx_g = 0; gfx_b = 0; gfx_a = 0.125;
	gfx_rect(ix, iy, iw, ih, 0);
	gfx_r = 1; gfx_g = 1; gfx_b = 1; gfx_a = 0.5;
	gfx_rect(ix, iy, iw, ih, 0);
	gfx_r = 0; gfx_g = 0; gfx_b = 0; gfx_a = 1;
	gfx_rect(ix-1, iy-1, iw+2, ih+2, 0);
);

function blit_border(ix iy iw ih hue sat lum)
(
	hsla(hue,0,0,0.125);
	gfx_rect(ix, iy, iw, ih, 0);
	hsla(hue,sat,lum,1);
	gfx_rect(ix, iy, iw, ih, 0);
	hsla(hue,sat,lum/8,1);
	gfx_rect(ix-1, iy-1, iw+2, ih+2, 0);
);

function blit_debug_border(ix iy iw ih hue thicknessLeft thicknessRight thicknessTop thicknessBottom)
(
	GUI_RENDER_DEBUG_BORDERS == 1 ? (
		hsla(hue,0.5,0.5,1);
		gfx_rect(ix, iy, iw, thicknessTop);
		gfx_rect(ix, iy + ih - thicknessBottom, iw, thicknessBottom);
		gfx_rect(ix, iy, thicknessLeft, ih);
		gfx_rect(ix + iw - thicknessRight, iy, thicknessRight, ih);
	);
);

function blit_debug_border(ix iy iw ih hue thickness)
(
	blit_debug_border(ix, iy, iw, ih, hue, thickness, thickness, thickness, thickness);
);

function layout()
(
	this.address = UI_BUFFER_ADDR_BASE;
	this.main = UI_BUFFER_ADDR_MAIN;
	this.sub = UI_BUFFER_ADDR_SUB;
	this.status.status();
);

function layout_subgrid_select(c r col_span row_span)
local(x y w h bx by bw bh)
(
	x = this.main_x + THEME_PADDING_MAIN_LEFT;
	y = this.main_y + THEME_PADDING_MAIN_TOP;
	w = this.main_w - THEME_PADDING_MAIN_LEFT - THEME_PADDING_MAIN_RIGHT;
	h = this.main_h - THEME_PADDING_MAIN_TOP - THEME_PADDING_MAIN_BOTTOM;
	bx = (w / this.sub_cols) * c;
	by = (h / this.sub_rows) * r;
	bw = w / this.sub_cols;
	bh = h / this.sub_rows;
	(col_span > 1) ? bw *= col_span;
	(row_span > 1) ? bh *= row_span;
	bx += THEME_PADDING_SUB / 2;
	bw -= THEME_PADDING_SUB;
	by += THEME_PADDING_SUB / 2;
	bh -= THEME_PADDING_SUB;
	bx += x;
	by += y;
	this.sub.selection.x = snapXToPixels(bx);
	this.sub.selection.y = snapYToPixels(by);
	this.sub.selection.h = snapWidthToPixels(bh);
	this.sub.selection.w = snapHeightToPixels(bw);
	blit_debug_border(
		bx,by,bw,bh,
		30+(r+c)*2,
		2
	);
);

function layout_grid_select(c r cols rows)
(
	this.main.selection.w = snapXToPixels(this.main_w - THEME_PADDING_MAIN_LEFT - THEME_PADDING_MAIN_RIGHT);
	this.main.selection.h = snapYToPixels(this.main_h - THEME_PADDING_MAIN_TOP - THEME_PADDING_MAIN_BOTTOM);
	this.main.selection.x = snapWidthToPixels((this.main.selection.w / this.main_cols) * c + THEME_PADDING_MAIN_LEFT);
	this.main.selection.y = snapHeightToPixels((this.main.selection.h / this.main_crows) * r + THEME_PADDING_MAIN_TOP);
);

function layout_grid(x y w h cols rows)
(
	this.main = this.address;
	this.main_subgrids = 0;
	this.main_x = x;
	this.main_y = y;
	this.main_w = w;
	this.main_h = h;
	this.main_cols = cols;
	this.main_rows = rows;
	this.layout_grid_select(0, 0, cols, rows);
);

function layout_subgrid(s_cols s_rows)
(
	this.sub = this.address + 25 + this.main_subgrids * 25;
	this.main_subgrids += 1;
	this.sub_cols = s_cols;
	this.sub_rows = s_rows;
	this.layout_subgrid_select(0, 0, s_cols, s_rows);
);

function layout_hover()
(
	mouseInArea(this.sub.selection.x, this.sub.selection.y, this.sub.selection.w, this.sub.selection.h);
);

function layout_click()
(
	mouseClickIn(this.sub.selection.x, this.sub.selection.y, this.sub.selection.w, this.sub.selection.h);
);

function layout_hover(sub_x sub_y div_x div_y)
(
	mouseInArea(ceil(this.sub.selection.x+sub_x*floor(this.sub.selection.w/div_x))+1,
	ceil(this.sub.selection.y+sub_y*floor(this.sub.selection.h/div_y))+1,
	floor(this.sub.selection.w/div_x)-2, floor(this.sub.selection.h/div_y)-2);
);

function layout_click(sub_x sub_y div_x div_y)
(
	mouseClickIn(ceil(this.sub.selection.x+sub_x*floor(this.sub.selection.w/div_x))+1,
	ceil(this.sub.selection.y+sub_y*floor(this.sub.selection.h/div_y))+1,
	floor(this.sub.selection.w/div_x)-2, floor(this.sub.selection.h/div_y)-2);
);

function layout_alt_click()
(
	mouseALTClickIn(this.sub.selection.x, this.sub.selection.y, this.sub.selection.w, this.sub.selection.h);
);

function layout_wheel()
(
	mouseWheelIn(this.sub.selection.x, this.sub.selection.y, this.sub.selection.w, this.sub.selection.h);
);

function layout_slider_mouse_events(sliderN minVal maxVal defVal stepSize)
(
	this.layout_click() == 1 ?
	(
		slider(sliderN) = round(denormalize((mouse_x - this.sub.selection.x) / this.sub.selection.w, minVal, maxVal), stepSize);
		this.needs_update = 1;
	);
	this.layout_alt_click() == 1 ?
	(
		slider(sliderN) = defVal;
		this.needs_update = 1;
	);
	this.layout_wheel() == 1 ?
	(
		slider(sliderN) += (mouse_wheel/120) * stepSize;
		slider(sliderN) = clamp(round(slider(sliderN), stepSize), minVal, maxVal);
		this.needs_update = 1;
		mouse_wheel = 0;
	);
);

function layout_grid_background()
(
	GUI_RENDER_GRID_BACKGROUND == 1 ?
	(
		gfx_a = 1;
		blit_tiled_display(this.main_x, this.main_y, this.main_w, this.main_h, THEME_COLORS_MAIN_HUE, THEME_COLORS_MAIN_SAT, THEME_COLORS_MAIN_LUM, 1);
	);
);

function layout_subgrid_background()
(
	GUI_RENDER_SUBGRID_BACKGROUND == 1 ?
	(
		gfx_a = 1;
		blit_tiled_display(this.sub.selection.x, this.sub.selection.y, this.sub.selection.w, this.sub.selection.h, THEME_COLORS_DISPLAY_HUE, THEME_COLORS_SUB_SAT, THEME_COLORS_SUB_LUM, 1);
	);
);

function layout_slider(sliderN minVal maxVal defVal stepSize divisions title scale precision unit c r cols rows)
local(ix iy iw ih sx sy valNorm start_pos dx zcrossx dw dh zero_cross div_size idiv dpos str val)
(
	this.layout_subgrid_select(c, r, cols, rows);
	this.layout_subgrid_background();
	GUI_RENDER_COMPONENTS == 1 ?
	(
		this.needs_update = 0;
		ix = this.sub.selection.x;
		iy = this.sub.selection.y;
		iw = this.sub.selection.w;
		ih = this.sub.selection.h;
		sx = ix;
		sy = iy;
		valNorm = normalize(slider(sliderN), minVal, maxVal);
		start_pos = minVal < 0 ? (maxVal > 0 ? "center" : "right") : "left";
		start_pos == "left" ?
		(
			sx = ix;
			dx = ix + valNorm * iw;
		) :
		(
			start_pos == "right" ?
			(
				sx = ix + iw;
				dx = ix + (iw - (1-valNorm) * iw);
			) :
			(
				zcrossx = ix + zeroCrossing(minVal, maxVal) * iw;
				sx = zcrossx;
				dx = ix + valNorm * iw;
			)
		);
		sx > dx ? atomic_exch(sx, dx);
		dw = dx - sx;
		dh = ih;
		zero_cross = zeroCrossing(minVal, maxVal);
		div_size = iw/divisions;
		start_pos == "center" ? (
			dw += sx-floor(sx);
			dw = ceil(dw);
		);
		blit_tiled_display(sx, sy, dw-1, dh, THEME_COLORS_SUB_HUE_FG+(this.layout_hover()*THEME_COLORS_SHIFT_HOVER_HUE), THEME_COLORS_SUB_SAT_DIM, THEME_COLORS_SUB_LUM_DIM, 1);
		hsla(THEME_COLORS_SUB_HUE, 0.5, 0.5, 0.35);
		idiv = 0;
		while (idiv < divisions)
		(
			dpos = ix + (idiv + (start_pos == "center" ? zero_cross + 0.5 : 0)) * div_size;
			dpos >= ix && dpos <= ix+iw ? gfx_line(dpos, iy, dpos, iy+ih);
			idiv += 1;
		);
		start_pos == "center" ?
		(
			dpos = zcrossx + (sx < zcrossx ? -dw : dw);
		) :
		(
			dpos = ix + dw;
		);
		dpos < ix + 2 ? ( dpos = ix + 2; );
		dpos > ix + iw - 2 ? ( dpos = ix + iw - 2; );
		hsla(THEME_COLORS_SUB_HUE_FG, 0.5, 0.5 ,0.5);
		gfx_line(dpos-1, iy, dpos-1, iy + ih);
		gfx_line(dpos+1, iy, dpos+1, iy + ih);
		hsla(THEME_COLORS_SUB_HUE_FG, 0.5, 0.5 ,0.75);
		gfx_line(dpos, iy, dpos, iy + ih);
		str = #;
		val = (valNorm-zero_cross);
		start_pos == "right" ?
		(
			val = (1-val)-zero_cross;
		) :
		(
			start_pos == "left" ?
			(
				val = val+zero_cross;
			) :
			(
				val < 0 ?
				(
					val = -(1-valNorm/zero_cross);
				) :
				(
					val = (valNorm-zero_cross)/(1-zero_cross);
				);
			);
		);
		val *= scale;
		sprintf(str, "%s: %s", title, formatNumber(val, precision, unit, 0));
		drawStringCentered(this.sub.selection.x, this.sub.selection.y, this.sub.selection.w, this.sub.selection.h, str, THEME_COLORS_SUB_HUE_LABEL, THEME_COLORS_SUB_SAT_DIM, 0.7, 0.9);
		this.layout_slider_mouse_events(sliderN, minVal, maxVal, defVal, stepSize);
		blit_border(ix, iy, iw, ih);
		this.needs_update = 1;
	);
);

function layout_xy_pad(sliderX sliderY minXVal maxXVal minYVal maxYVal stepSizeX stepSizeY defXVal defYVal divisions title label_top_left label_top_right label_bottom_left label_bottom_right c r cols rows)
local(ix iy iw ih valX valY i dx dx2 dy dy2 ix2 iy2 iw2 ih2 xDiv wDiv yDiv hDiv id x1 y1 _dx1 _dx2 _dy1 _dy2 _x1 _x2 _x3 _x4 _x5 _x6 _x7 _y1 _y2 _y3 _y4 _y5 _y6 label_mix)
(
	this.layout_subgrid_select(c, r, cols, rows);
	this.layout_subgrid_background();
	GUI_RENDER_COMPONENTS == 1 ?
	(
		this.needs_update = 0;
		ix = this.sub.selection.x;
		iy = this.sub.selection.y;
		iw = this.sub.selection.w;
		ih = this.sub.selection.h;
		valX = normalize(slider(sliderX), minXVal, maxXVal);
		valY = normalize(slider(sliderY), minYVal, maxYVal);
		hsla(THEME_COLORS_DISPLAY_HUE, THEME_COLORS_SUB_SAT, THEME_COLORS_SUB_LUM, 1);
		gfx_rect(ix, iy, iw, ih, 1);
		i = 0;
		dx = iw * valX;
		dx2 = iw * (1-valX);
		dy = ih - ih * valY;
		dy2 = ih - ih * (1-valY);
		ix2 = ix + iw;
		iy2 = iy + ih;
		iw2 = 0.5 * iw;
		ih2 = 0.5 * ih;
		xDiv = iw * valX / divisions;
		wDiv = iw / divisions;
		yDiv = ih * valY / divisions;
		hDiv = ih / divisions;
		while (i <= divisions)
		(
			id = i / divisions;
			x1 = i * wDiv;
			y1 = i * hDiv;
			_dx1 = ix + dx;
			_dx2 = ix2 - dx2;
			_dy1 = iy + dy;
			_dy2 = iy2 - dy2;
			_x1 = ix + x1;
			_x2 = ix + x1 / 2;
			_x3 = ix + i * xDiv;
			_x4 = ix + (iw2 + iw * valX * id - iw2 * id);
			_x5 = ix2 - x1;
			_x6 = ix2 - x1 / 2;
			_x7 = ix2 - (x1 - i * xDiv);
			_y1 = iy + y1;
			_y2 = iy + y1 / 2;
			_y3 = iy + (y1 - i * yDiv);
			_y4 = iy + (ih2 - ih * valY * id + ih2 * id);
			_y5 = iy2 - y1 / 2;
			_y6 = iy2 - i * yDiv;
			hsla(THEME_COLORS_DISPLAY_HUE, THEME_COLORS_DISPLAY_SAT, THEME_COLORS_DISPLAY_LUM, 1);
			gfx_rect(ix + 5, iy2 - valY * ih + 5, 2, ih * valY - 10, 1);
			gfx_rect(ix + 5, iy2 - 5, iw * valX - 10, 2, 1);
			gfx_line(ix+iw2, iy2, _dx1, _dy1);
			gfx_line(ix, iy+ih2, _dx1, _dy1);
			gfx_line(ix+iw2, iy, _dx1, _dy1);
			gfx_line(ix2, iy+ih2, _dx1, _dy1);
			hsla(THEME_COLORS_DISPLAY_HUE, THEME_COLORS_DISPLAY_SAT, THEME_COLORS_DISPLAY_LUM, 0.3);
			gfx_line(_x5, iy, _dx1, _dy1);
			gfx_line(ix2, iy, _x4, _y3);
			gfx_line(ix, iy, _x4, _y3);
			gfx_line(_x6, iy, _dx1, _dy1);
			gfx_line(_x2, iy, _dx1, _dy1);
			gfx_line(_x1, iy2, _dx1, _dy2);
			gfx_line(ix2, iy2, _x4, _y6);
			gfx_line(ix, iy2, _x4, _y6);
			gfx_line(_x2, iy2, _dx1, _dy2);
			gfx_line(_x6, iy2, _dx1, _dy2);
			gfx_line(ix2, _y1, _dx2, _dy1);
			gfx_line(ix2, iy, _x7, _y4);
			gfx_line(ix2, iy2, _x7, _y4);
			gfx_line(ix2, _y2, _dx2, _dy1);
			gfx_line(ix2, _y5, _dx2, _dy1);
			gfx_line(ix, _y1, _dx1, _dy1);
			gfx_line(ix, iy, _x3, _y4);
			gfx_line(ix, iy2, _x3, _y4);
			gfx_line(ix, _y2, _dx1, _dy1);
			gfx_line(ix, _y5, _dx1, _dy1);
			i += 1;
		);
		dx = max(0, min(iw, dx));
		dy = max(0, min(ih, dy));
		dx2 = max(0, min(iw, dx2));
		dy2 = max(0, min(ih, dy2));
		gfx_r = 1; gfx_g = 1; gfx_b = 1; gfx_a = 0.75;
		gfx_line(ix, iy, ix + dx, iy + dy);
		gfx_line(ix + iw, iy, ix + dx, iy + dy);
		gfx_line(ix, iy + ih, ix + dx, iy + dy);
		gfx_line(ix + iw, iy + ih, ix + dx, iy + dy);
		gfx_circle(ix + dx, iy + dy, 3, 1);
		blit_border(ix, iy, iw, ih);
		label_mix = #;
		label_mix = sprintf(label_mix, "%3d%% %-10s\n\n%3d%% %-10s", valY * 100, label_top_left, valX * 100, label_bottom_right);
		gfx_measurestr(label_top_left, w, h);
		drawStringOutlined(ix+10, iy+10, label_top_left, THEME_COLORS_SUB_HUE_LABEL, THEME_COLORS_SUB_SAT_DIM, THEME_COLORS_SUB_LUM_DIM+(valY*(1-valX)), 1);
		gfx_measurestr(label_top_right, w, h);
		drawStringOutlined(ix+(iw-10-w), iy+10, label_top_right, THEME_COLORS_SUB_HUE_LABEL, THEME_COLORS_SUB_SAT_DIM, THEME_COLORS_SUB_LUM_DIM+(valY*valX), 1);
		gfx_measurestr(label_bottom_left, w, h);
		drawStringOutlined(ix+10, iy+(ih-10-h), label_bottom_left, THEME_COLORS_SUB_HUE_LABEL, THEME_COLORS_SUB_SAT_DIM, THEME_COLORS_SUB_LUM_DIM+((1-valY)*(1-valX)), 1);
		gfx_measurestr(label_bottom_right, w, h);
		drawStringOutlined(ix+(iw-10-w), iy+(ih-10-h), label_bottom_right, THEME_COLORS_SUB_HUE_LABEL, THEME_COLORS_SUB_SAT_DIM, THEME_COLORS_SUB_LUM_DIM+((1-valY)*valX), 1);
		gfx_measurestr(label_mix, w, h);
		drawStringOutlined(ix+iw2-w/2, iy+ih2-h/2, label_mix, THEME_COLORS_SUB_HUE_LABEL, THEME_COLORS_SUB_SAT_DIM, THEME_COLORS_SUB_LUM_DIM+(valY*valX), 1);
		this.layout_hover() ? showToolTip(mouse_x, mouse_y-10, 100, 20, title);
		this.layout_click() ?
		(
			slider(sliderX) = round(denormalize(snapNormalizedToEdge((mouse_x - this.sub.selection.x) / this.sub.selection.w, 0.015), minXVal, maxXVal), stepSizeX);
			slider(sliderY) = round(denormalize(snapNormalizedToEdge(1 - (mouse_y - this.sub.selection.y) / this.sub.selection.h, 0.015), minYVal, maxYVal), stepSizeY);
			this.needs_update = 1;
		);
		this.layout_alt_click() ?
		(
			slider(sliderX) = defXVal;
			slider(sliderY) = defYVal;
			this.needs_update = 1;
		);
	);
);

function layout_frame()
(
	GUI_RENDER_FRAME == 1 ?
	(
		gfx_a = 0.25;
		gfx_r = 0;
		gfx_g = 0;
		gfx_b = 0;
		gfx_rect(this.main_x, this.main_y, THEME_FRAME_LEFT, this.main_h);
		gfx_rect(this.main_x + this.main_w - THEME_FRAME_RIGHT, this.main_y, THEME_FRAME_RIGHT, this.main_h);
		gfx_rect(this.main_x + THEME_FRAME_LEFT, this.main_y, this.main_w - THEME_FRAME_RIGHT - THEME_FRAME_LEFT, THEME_FRAME_TOP);
		gfx_rect(this.main_x + THEME_FRAME_LEFT, this.main_y + this.main_h - THEME_FRAME_BOTTOM, this.main_w - THEME_FRAME_RIGHT - THEME_FRAME_LEFT, THEME_FRAME_BOTTOM);
		gfx_a = 1;
		blit_border(this.main_x+1, this.main_y+1, this.main_w-2, this.main_h-2, THEME_COLORS_MAIN_HUE, THEME_COLORS_MAIN_SAT, THEME_COLORS_MAIN_LUM);
	);
);
ui.layout();
BUFFERS_MAX = 100;
GFX_BUFFERS_MAX = 1000;
GFX_BUFFER_SIZE = 11;
UI_BUFFER_SIZE = 25;
GONIOMETER_BUFFER_SIZE = 2048;
UI_BUFFER_ADDR_BASE = 16384*10;
UI_BUFFER_ADDR_MAIN = UI_BUFFER_ADDR_BASE;
UI_BUFFER_ADDR_SUB = UI_BUFFER_ADDR_BASE + UI_BUFFER_SIZE;
GFX_BUFFER_ADDR_BASE = UI_BUFFER_ADDR_BASE + UI_BUFFER_SIZE * 4;
GFX_OFF_SCREEN_ADDR_BASE = GFX_BUFFER_ADDR_BASE + GFX_BUFFERS_MAX * GFX_BUFFER_SIZE;
GONIOMETER_BUFFER_ADDR_BASE = GFX_OFF_SCREEN_ADDR_BASE + GFX_BUFFERS_MAX * GFX_BUFFER_SIZE;
GONIOMETER_BUFFER_ADDR_X = GONIOMETER_BUFFER_ADDR_BASE;
GONIOMETER_BUFFER_ADDR_Y = GONIOMETER_BUFFER_ADDR_X + GONIOMETER_BUFFER_SIZE * 2 + 2;
BUFFER_ADDR_BASE = GONIOMETER_BUFFER_ADDR_Y + GONIOMETER_BUFFER_SIZE * 2 + 2;
BUFFER_ADDR_HEADERS = BUFFER_ADDR_BASE;
BUFFER_ADDR_ADDRESSES = BUFFER_ADDR_BASE + BUFFERS_MAX;
BUFFER_ADDR_DATA = BUFFER_ADDR_BASE + BUFFERS_MAX * 2;
ST.status();

function compressSample(s threshold)
(
	s = (-0.2397 * s + 1.0599 * s) * threshold;
	s;
);

function compressSample(s)
(
	compressSample(s, compThreshold);
);

function compress(s0 s1 threshold)
(
	s0 = compressSample(s0, threshold);
	s1 = compressSample(s1, threshold);
);

function compress(s0 s1)
(
	compress(s0, s1, compThreshold);
);

function compress(threshold)
(
	spl0 = (-0.2397 * spl0 * abs(spl0) + 1.0599 * spl0) * threshold;
	spl1 = (-0.2397 * spl1 * abs(spl1) + 1.0599 * spl1) * threshold;
);

function compress()
(
	compress(compThreshold);
);

function compressor(rmsSpeed compAttack)
(
	rms0.smoothing_rms(rmsSpeed);
	rms1.smoothing_rms(rmsSpeed);
	sp0.smoothing_single_pole(compAttack,1);
);

function compressor_run(modify_dynamics)
local(compThreshold dyn)
(
	rmsspl0 = rms0.smoothing_rms_sample(spl0);
	rmsspl1 = rms1.smoothing_rms_sample(spl1);
	compThreshold = sp0.smoothing_single_pole_sample(compThreshold, 0.95 - max(rmsspl0, rmsspl1)/8);
	modify_dynamics ?
	(
		dyn = 0.05*compThreshold;
		(dyn != 0) ?
		(
			spl0 += (-sign(spl0) * sqr(spl0)) * dyn;
			spl1 += (-sign(spl1) * sqr(spl1)) * dyn;
		);
	);
	spl0 = compressSample(spl0, compThreshold);
	spl1 = compressSample(spl1, compThreshold);
);

function compressor_run()
(
	this.compressor_run(0);
);

function stereo_enhancer()
(
	this.x = exp(-2.0 * $pi * 356 / srate);
	this.x2 = this.x * 2.0;
	this.a0 = 1.0 - this.x;
	this.b1 = - this.x;
);

function stereo_enhancer_run(mod)
local(s0 s1 mono stereo mono_lp stereo_lp)
(
	s0 = spl0;
	s1 = spl1;
	s0 = (s0 + spl0) / this.x2 + spl0 / this.x;
	s1 = (s1 + spl1) / this.x2 + spl1 / this.x;
	spl0 = this.t0 = this.a0 * spl0 - this.b1 * this.t0;
	spl1 = this.t1 = this.a0 * spl1 - this.b1 * this.t1;
	s0 -= spl0;
	s1 -= spl1;
	mono = (s0 + s1) / 2;
	stereo = (s0 - s1);
	s0 = mono + stereo * (mod + 0.94);
	s1 = mono - stereo * (mod + 0.94);
	mono_lp = (spl0 + spl1) / 2;
	stereo_lp = (spl0 - spl1);
	spl0 = mono_lp + stereo_lp * (mod + 0.29);
	spl1 = mono_lp - stereo_lp * (mod + 0.29);
	spl0 -= s0;
	spl1 -= s1;
);

function mid_side(balance panning)
local(pan bal)
(
	pan = clamp(panning, 0, 1);
	bal = clamp(balance, 0, 1);
	this.volScale = bal >= 0 ? (1 - bal / 2) : (1 - bal / 2);
	this.pan0 = 1 + -pan;
	this.pan1 = 1 + pan;
	this.bal0 = 1 + -bal;
	this.bal1 = 1 + bal;
);

function mid_side_run()
local(l0 l1 l r)
(
	l0 = spl0;
	l1 = spl1;
	l = l0 + l1;
	r = l0 + -l1;
	l = l * this.bal0;
	r = r * this.bal1;
	l0 = l;
	l1 = r;
	l = 0.5 * l0 + 0.5 * l1;
	r = 0.5 * l0 + -0.5 * l1;
	spl0 = (l * this.volScale) * this.pan0;
	spl1 = (r * this.volScale) * this.pan1;
);

function stereoizer()
(
	this.us = -0.5001;
	this.enhancer.stereo_enhancer();
);

function stereoizer_run()
local(ds0 ds1 deltaS pMod pan correction t0 t1)
(
	ds0 = spl0 * this.us;
	ds1 = spl1 / this.us;
	deltaS = max(ds0, ds1) - min(ds0, ds1);
	pMod = clamp(this.us * deltaS * 100, -2, 2);
	pan = (0.5 * pMod - pMod) * (-0.5 * deltaS + deltaS);
	correction = clampNormCenter(sqr(spl0) * sqr(spl1))*(COS_OF_ONE*cos(spl0+spl1));
	midSide.mid_side(correction * 10, pan * 2);
	midSide.mid_side_run();
	t0 = spl0;
	t1 = spl1;
	spl0 *= -1;
	this.enhancer.stereo_enhancer_run(correction);
	this.dst = compressSample(-0.05 * ((t0 - spl0) - (t1 - spl1)) / this.us) / 2;
	spl0 = t0;
	spl1 = t1;
);

function stereoizer_get_fx()
(
	this.dst;
);

function sonic_clean(cleanLevel)
(
	this.is_q = 3.6514837167;
	this.us_q = 0.23693063937625;
	this.is_f = 5.80;
	this.clean_strength_is = cleanLevel;
	this.clean_strength_us = cleanLevel;
);

function sonic_clean_applyDenorm(spl)
(
	spl + (10^-30);
);

function sonic_clean_correctDenorm(spl)
(
	abs(spl) < (10^-30) ? 0 : spl;
);

function sonic_clean_setEQBandSS(bandGain bandFreq)
local(_w0)
(
	_w0 = (2 * $pi) * bandFreq/srate;
	this._cos_w0 = cos(_w0);
	this._alpha = sin(_w0) / 1.82574185835;
);

function sonic_clean_setEQBandUS(bandGain bandFreq)
local(_w0)
(
	_w0 = (2 * $pi) * bandFreq/srate;
	this._cos_w0 = cos(_w0);
	this._alpha = sin(_w0) / 0.840832920383;
);

function sonic_clean_setEQBandHP()
(
	this._a0 = 1 + this._alpha;
);

function sonic_clean_setEQBandLP()
(
	this._a0 = 1 + this._alpha;
);

function sonic_clean_hpfB0()
(
	(1 + this._cos_w0)/2;
);

function sonic_clean_hpfB1()
(
	-(1 + this._cos_w0);
);

function sonic_clean_hpfB2()
(
	(1 + this._cos_w0)/2;
);

function sonic_clean_hpfA1()
(
	-2 * this._cos_w0;
);

function sonic_clean_hpfA2()
(
	1 - this._alpha;
);

function sonic_clean_lpfB0()
(
	(1 - this._cos_w0)/2;
);

function sonic_clean_lpfB1()
(
	(1 - this._cos_w0);
);

function sonic_clean_lpfB2()
(
	(1 - this._cos_w0)/2;
);

function sonic_clean_lpfA1()
(
	-2 * this._cos_w0;
);

function sonic_clean_lpfA2()
(
	1 - this._alpha;
);

function sonic_clean_applyEQ(s b0 b1 b2 a1 a2 x1 x2 y1 y2)
(
	b0 * s + b1 * x1 + b2 * x2 - a1 * y1 - a2 * y2;
);

function sonic_clean_init()
local(us_f)
(
	us_f = min(20000, srate/2);
	this.sonic_clean_setEQBandSS(-84, this.is_f+this.is_f*this.is_q*this.clean_strength_is);
	this.sonic_clean_setEQBandHP();
	this.is_b0 = this.sonic_clean_hpfB0()/this._a0; this.is_b1 = this.sonic_clean_hpfB1()/this._a0; this.is_b2 = this.sonic_clean_hpfB2()/this._a0;
	this.is_a1 = this.sonic_clean_hpfA1()/this._a0; this.is_a2 = this.sonic_clean_hpfA2()/this._a0;
	this.sonic_clean_setEQBandUS(-84, us_f-us_f*this.us_q*this.clean_strength_us);
	this.sonic_clean_setEQBandLP();
	this.us_b0 = this.sonic_clean_lpfB0()/this._a0; this.us_b1 = this.sonic_clean_lpfB1()/this._a0; this.us_b2 = this.sonic_clean_lpfB2()/this._a0;
	this.us_a1 = this.sonic_clean_lpfA1()/this._a0; this.us_a2 = this.sonic_clean_lpfA2()/this._a0;
);

function sonic_clean_filter()
local(t0 t1 tmpSpl)
(
	t0 = spl0;
	t1 = spl1;
	tmpSpl = t0;
	t0 = this.sonic_clean_applyEQ(t0, this.is_b0, this.is_b1, this.is_b2, this.is_a1, this.is_a2, this.is_l_x1, this.is_l_x2, this.is_l_y1, this.is_l_y2);
	this.is_l_x2 = this.is_l_x1; this.is_l_x1 = tmpSpl;
	this.is_l_y2 = this.is_l_y1; this.is_l_y1 = this.sonic_clean_correctDenorm(t0);
	tmpSpl = t1;
	t1 = this.sonic_clean_applyEQ(t1, this.is_b0, this.is_b1, this.is_b2, this.is_a1, this.is_a2, this.is_r_x1, this.is_r_x2, this.is_r_y1, this.is_r_y2);
	this.is_r_x2 = this.is_r_x1; this.is_r_x1 = tmpSpl;
	this.is_r_y2 = this.is_r_y1; this.is_r_y1 = this.sonic_clean_correctDenorm(t1);
	tmpSpl = t0;
	t0 = this.sonic_clean_applyEQ(t0, this.us_b0, this.us_b1, this.us_b2, this.us_a1, this.us_a2, this.us_l_x1, this.us_l_x2, this.us_l_y1, this.us_l_y2);
	this.us_l_x2 = this.us_l_x1; this.us_l_x1 = tmpSpl;
	this.us_l_y2 = this.us_l_y1; this.us_l_y1 = this.sonic_clean_correctDenorm(t0);
	tmpSpl = t1;
	t1 = this.sonic_clean_applyEQ(t1, this.us_b0, this.us_b1, this.us_b2, this.us_a1, this.us_a2, this.us_r_x1, this.us_r_x2, this.us_r_y1, this.us_r_y2);
	this.us_r_x2 = this.us_r_x1; this.us_r_x1 = tmpSpl;
	this.us_r_y2 = this.us_r_y1; this.us_r_y1 = this.sonic_clean_correctDenorm(t1);
	spl0 = this.sonic_clean_applyDenorm(t0);
	spl1 = this.sonic_clean_applyDenorm(t1);
);

function buffer_manager()
(
	this.header_addresses = BUFFER_ADDR_HEADERS;
	this.header_lengths = BUFFER_ADDR_ADDRESSES;
	this.data_start = BUFFER_ADDR_DATA;
	this.data_size = 0;
	this.buffers_in_use = -1;
	this.buffers_max = BUFFERS_MAX;
);

function buffer_manager_address(id)
(
	this.header_addresses[id];
);

function buffer_manager_length(id)
(
	this.header_lengths[id];
);

function buffer_manager_size()
local(l)
(
	l = this.buffers_in_use-1;
	this.data_size = 0;
	while (l >= 0)
	(
		this.data_size += this.header_lengths[l];
		l -= 1;
	);
	this.data_size;
);

function buffer_manager_new(len)
local(s addr)
(
	this.buffers_in_use += 1;
	s = this.data_start;
	s += this.buffer_manager_size();
	this.__calc_addr = s;
	this.header_lengths[this.buffers_in_use] = len;
	this.header_addresses[this.buffers_in_use] = s;
	addr = this.header_addresses[this.buffers_in_use];
	len -= 1;
	while (len >= 0)
	(
		addr[l] = 0;
		l -= 1;
	);
	this.buffers_in_use;
);
BM.buffer_manager();

function buffer(len)
(
	this.buffer = BM.buffer_manager_new(len);
	this.address = BM.buffer_manager_address(this.buffer);
	this.address_end = BM.buffer_manager_address(this.buffer) + len;
	this.length = BM.buffer_manager_length(this.buffer);
	this.length_init = BM.buffer_manager_length(this.buffer);
	this.index = 0;
	this.index_reverse = this.length - 1;
);

function buffer_next()
(
	this.index += 1;
	this.index >= this.length-1 ? ( this.index = 0; );
	this.index_reverse = this.length - this.index - 1;
);

function buffer_wrap_index(new_index)
(
	this.index > this.length - 1 ?
	(
		this.index = 0;
	);
	new_index != this.index ?
	(
		new_index > this.length - 1 ?
		(
			while (new_index > this.length - 1)
			(
				new_index -= this.length;
			);
		) :
		(
			new_index < 0 ?
			(
				while (new_index < 0)
				(
					new_index += this.length;
				);
			);
		);
	);
	new_index;
);

function buffer_length(len)
local(new_index)
(
	len != this.length ?
	(
		len > this.length_init ? len = this.length_init;
		this.length = len;
		new_index = this.buffer_wrap_index(this.index);
		this.index = new_index;
		this.index_reverse = this.length - this.index - 1;
		this.length_squared = this.length * this.length;
	);
);

function buffer_get_fwd(o)
(
	this.address[this.buffer_wrap_index(this.index+o)];
);

function buffer_get_rev(o)
(
	this.address[this.buffer_wrap_index(this.index_reverse+o)];
);

function buffer_get_rev_delayed()
(
	ST.samples_played >= this.length ?
	(
		res = this.buffer_get_rev(1);
	) :
	(
		ST.samples_played >= floor(this.length/2) ?
		(
			res = this.buffer_get_rev(1) * ((ST.samples_played-floor(this.length/2))/(this.length/2));
		) :
		(
			res = 0;
		);
	);
	res;
);

function buffer_get_fwd_delayed()
(
	this.buffer_get_fwd(-1);
);

function buffer_blend(x amount_old amount_new)
(
	this.address[this.index] = this.address[this.index] * amount_old + x * amount_new;
	this.address[this.index];
);

function buffer_compress()
(
	this.address[this.index] = compressSample(this.address[this.index]);
);

function autopan(speed reaction_speed)
local(rms attack last_pan)
(
	rms = speed;
	attack = reaction_speed;
	rms0.smoothing_rms(rms);
	rms1.smoothing_rms(rms);
	sp0.smoothing_single_pole(attack,1);
	last_pan = pan = 0;
);

function autopan_run()
local(pan_delta last_pan)
(
	pan_delta = (last_pan - pan);
	pan_delta < 0 ?
	(
		spl0 *= (1+pan_delta+0.09);
		spl1 *= (1-pan_delta-0.09);
	):
	(
		spl0 *= (1-pan_delta+0.09);
		spl1 *= (1+pan_delta-0.09);
	);
	last_pan = pan;
);

function autopan_sample()
local(pan)
(
	rmsspl0 = rms0.smoothing_rms_sample(spl0);
	rmsspl1 = rms1.smoothing_rms_sample(spl1);
	pan = sp0.smoothing_single_pole_sample(pan, min(rmsspl0 / rmsspl1, rmsspl1 / rmsspl0));
);
// iir_mag = i1*i1 + i2*i2;

function complex_resonator(freq)
(
	this.i1 = 0;
	this.i2 = 0;
	this.angle = 2 * $PI * freq / srate;
	this.atten = 0.995;
	this.pole1 = cos(this.angle) * this.atten;
	this.pole2 = sin(this.angle) * this.atten;
);

function complex_resonator_detect(input)
(
	this.i1l = this.i1;
	this.i2l = this.i2;
	this.i1 = this.i1l * this.pole1 - this.i2l * this.pole2 + input;
	this.i2 = this.i1l * this.pole2 + this.i2l * this.pole1;
	this.im = this.i1 * this.i1 + this.i2 * this.i2;
	this.im;
);
// iir_mag = i1*i1 + i2*i2;
gfx_clear = -1;
compThreshold = 0.95;
comp0.compressor(0.1, 50);
comp1.compressor(0.1, 50);
autopan.autopan(10, 100);
st.stereoizer();
stDyn = 1.0;
stDyn = -1.625 / 3;
sc.sonic_clean(1.75);
sc.sonic_clean_init();
sc_post.sonic_clean(1.75);
sc_post.sonic_clean_init();
rms0.smoothing_RMS(5);
rms1.smoothing_RMS(5);
buffers_initialized = 0;
buffers_initialized == 0 ?
(
	samples_total = 1536000;
	dummy_buf.buffer(1);
	buf.buffer(samples_total);
	buf_r.buffer(samples_total);
	buffers_initialized = 1;
);
bars = 1;
sl1l = 1;
sl4l = 0.8;
sl8l = 0.72;
sl9l = 0.72;
s1.signal();
s2.signal();
s1env = 0;
s2env = 0;

/*||||||||||||||||||||||||||||||||||||||*/

@slider
/*||||||||||||||||||||||||||||||||||||||*/
slider11 != lvlDry ? lvlDry = slider11;
slider12 != lvlWet ? lvlWet = slider12;
sl1l = slider1;
sl4l = slider4;
sl8l = slider8;
sl9l = slider9;

/*||||||||||||||||||||||||||||||||||||||*/

@block
/*||||||||||||||||||||||||||||||||||||||*/
vol_limit = abs(db2vol(-0.01));
bars = slider1;
bars < 0.0078125 ? ( bars = 0.0078125; );
samples = samplesNBars(bars);
buf.buffer_length(samples);
buf_r.buffer_length(samples);
sc.clean_strength_us = slider14;
sc.clean_strength_is = slider11;
sc.sonic_clean_init();
sc_post.clean_strength_us = slider14;
sc_post.clean_strength_is = 0.75;
sc_post.sonic_clean_init();
cr_l.complex_resonator(slider16);
cr_r.complex_resonator(slider16);

/*||||||||||||||||||||||||||||||||||||||*/

@sample
/*||||||||||||||||||||||||||||||||||||||*/
ST.status_update();
ST.status_is_playing() ?
(
	sc.sonic_clean_filter();
	autopan.autopan_sample();
	comp0.compressor_run();
	tmp0 = spl0;
	tmp1 = spl1;
	compress();
	st.stereoizer_run();
	compress(tmp0, tmp1);
	gain_rider = max(-1, min(1, slider15 * (rmsspl0+rmsspl1)/2));
	gain_rider_fwd = (1-gain_rider)*slider8;
	gain_rider_rev = (1-gain_rider)*slider9;
	gain_rider_fdbk = (1-gain_rider)*slider4;
	gain_rider_mix = max(abs(spl0), abs(spl1));
	gain_rider_mix_dry = gain_rider_mix*slider11;
	gain_rider_mix_wet = (1-gain_rider_mix)*slider12;
	slider13 = 0.5+(tmp0*tmp1)*3.5;
	slider14 = 0.5+gain_rider*3.5;
	buf.buffer_blend(tmp0, gain_rider_fdbk + s2env, 1);
	buf.buffer_compress();
	buf.buffer_next();
	buf_r.buffer_blend(tmp1, gain_rider_fdbk + s1env, 1);
	buf_r.buffer_compress();
	buf_r.buffer_next();
	sbl = 0;
	sbl += buf.buffer_get_fwd_delayed()*(gain_rider_fwd * (1+s2env));
	sbl += buf.buffer_get_rev_delayed()*(gain_rider_rev * (1+s2env));
	sbl *= gain_rider_mix_wet * (1+s1env);
	sbr = 0;
	sbr += buf_r.buffer_get_fwd_delayed()*(gain_rider_fwd * (1+s1env));
	sbr += buf_r.buffer_get_rev_delayed()*(gain_rider_rev * (1+s1env));
	sbr *= gain_rider_mix_wet * (1+s2env);
	tmp0 *= gain_rider_mix_dry * (1 - s2env);
	tmp1 *= gain_rider_mix_dry * (1 - s1env);
	tmp0 += st.stereoizer_get_fx();
	tmp1 -= st.stereoizer_get_fx();
	sbl -= st.stereoizer_get_fx()/(4-abs(sbl)*2);
	sbr += st.stereoizer_get_fx()/(4-abs(sbr)*2);
	spl0 = tmp0 + sbl;
	spl1 = tmp1 + sbr;
	autopan.autopan_run();
	comp1.compressor_run(1);
	compress();
	rmsspl0 = rms0.smoothing_RMS_sample(spl0);
	rmsspl1 = rms1.smoothing_RMS_sample(spl1);
	sc_post.sonic_clean_filter();
	spl0 = max(-vol_limit, min(vol_limit, spl0));
	spl1 = max(-vol_limit, min(vol_limit, spl1));
	tspl0 = cr_l.complex_resonator_detect(spl0)*0.00707;
	tspl1 = cr_r.complex_resonator_detect(spl1)*0.00707;
	s1env = s1.signal_envelope(tspl0, 0.25, 0.01);
	s2env = s2.signal_envelope(tspl1, 0.25, 0.01);
);

/*||||||||||||||||||||||||||||||||||||||*/

@gfx 260 360
/*||||||||||||||||||||||||||||||||||||||*/
gfx_w < 260 ? gfx_w = 260;
gfx_h < 360 ? gfx_h = 360;
ui.layout_grid(0, 0, gfx_w, gfx_h, 1, 1);
ui.layout_grid_background();
ui.layout_subgrid(1, 12);
ui.layout_xy_pad(9, 8, 0, 1, 0, 1, 0.01, 0.01, 1, 1, 10, "Delay Mix", "Fwd.", "Both", "None", "Rev.", 	0, 0, 1, 4);
ui.layout_xy_pad(11, 12, 0, 1, 0, 1, 0.01, 0.01, 1, 1, 10, "Output Mix", "Wet", "Both", "None", "Dry", 	0, 4, 1, 4);
ui.layout_slider(1, 0, 16, 0.75, 0.015625, 16, "Length", 16, 4, " bars", 	0, 8, 1, 1);
ui.layout_slider(4, 0, 1, 1, 0.01, 20, "Feedback", 100, 1, "%", 	0, 9, 1, 1);
ui.layout_slider(15, 0, 1, 1, 0.01, 20, "Gain Rider", 100, 1, "%", 	0, 10, 1, 1);
ui.layout_slider(16, 0, 20000, 1000, 1, 25, "Detector Freq.", 20000, 1, "Hz", 	0, 11, 1, 1);
ui.layout_frame();
mouse_wheel = 0;
